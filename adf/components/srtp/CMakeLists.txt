cmake_minimum_required(VERSION 3.12)

idf_component_register(REQUIRES mbedtls)

set(SRTP_INSTALL_DIR ${CMAKE_CURRENT_BINARY_DIR}/srtp_install)
idf_component_set_property(${COMPONENT_NAME} SRTP_PREFIX ${SRTP_INSTALL_DIR})

include(ExternalProject)

ExternalProject_Add(srtp
  SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/libsrtp
  CMAKE_ARGS
    -DENABLE_MBEDTLS=ON
    -DLIBSRTP_TEST_APPS=OFF
    -DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}
    -DBUILD_SHARED_LIBS=OFF
    -DENABLE_WARNINGS_AS_ERRORS=OFF
    -DMBEDTLS_INCLUDE_DIRS=$<TARGET_PROPERTY:${COMPONENT_TARGET},INTERFACE_INCLUDE_DIRECTORIES>
    -DMBEDTLS_LIBRARY=$<TARGET_FILE:mbedtls>
    -DMBEDX509_LIBRARY=$<TARGET_FILE:mbedx509>
    -DMBEDCRYPTO_LIBRARY=$<TARGET_FILE:mbedcrypto>
    -DCMAKE_PROJECT_libsrtp2_INCLUDE=${CMAKE_SOURCE_DIR}/cmake/add-extra-properties.cmake
    -DEXTRA_COMPILE_DEFINITIONS=$<TARGET_PROPERTY:${COMPONENT_TARGET},INTERFACE_COMPILE_DEFINITIONS>
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
  CMAKE_CACHE_ARGS
    -DCMAKE_EXPORT_COMPILE_COMMANDS:BOOL=ON
    -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
    -DHAVE_NETINET_IN_H:BOOL=ON
  INSTALL_DIR ${SRTP_INSTALL_DIR}
  BUILD_BYPRODUCTS ${SRTP_INSTALL_DIR}/lib/libsrtp2.a
)

add_prebuilt_library(srtp_lib "${SRTP_INSTALL_DIR}/lib/libsrtp2.a" REQUIRES mbedtls)
target_include_directories(srtp_lib INTERFACE "${SRTP_INSTALL_DIR}/include")
add_dependencies(srtp_lib srtp)
target_link_libraries(${COMPONENT_LIB} INTERFACE srtp_lib)
